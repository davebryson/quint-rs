use crate::{QuintEx, QuintDeclaration, QuintVar};
use crate::utils::QuintIdGenerator;
use crate::lexer::{Token, LexicalError};

grammar(helper: &mut QuintIdGenerator);


QuintDeclaration: QuintDeclaration = {
  "var" <name:"identifier"> ":" "int" => {
    QuintDeclaration::QuintVar(
      QuintVar{
        id: helper.get_id(), 
        name: name.into(), 
        imported_from: None, 
        namespaces: None
      }
    )
  }
}


// TODO: check precedence
pub QuintEx: QuintEx = {
    #[precedence(level="1")]
    ExpTerm,

    #[precedence(level="2")] #[assoc(side="left")]
    <lhs:QuintEx> "*" <rhs:QuintEx> => {
        QuintEx::QuintApp{
          id: helper.get_id(), 
          opcode: "imul".into(), 
          args: vec![lhs, rhs]
        }
    },
    #[precedence(level="2")] #[assoc(side="left")]
    <lhs:QuintEx> "/" <rhs:QuintEx> => {
        QuintEx::QuintApp{
          id: helper.get_id(), 
          opcode: "idiv".into(), 
          args: vec![lhs, rhs]
        }
    },
    #[precedence(level="3")] #[assoc(side="left")]
    <lhs:QuintEx> "+" <rhs:QuintEx> => {
        QuintEx::QuintApp{
          id: helper.get_id(), 
          opcode: "iadd".into(), 
          args: vec![lhs, rhs]
        }
    },
    <lhs:QuintEx> "-" <rhs:QuintEx> => {
        QuintEx::QuintApp{
          id: helper.get_id(), 
          opcode: "isub".into(), 
          args: vec![lhs, rhs]
        }
    },
}

ExpTerm: QuintEx = {
  <value:"int"> => {
    QuintEx::QuintInt{id: helper.get_id(), value}
  },
  <value:"str"> => {
    QuintEx::QuintStr{id: helper.get_id(), value: value.into()}
  },
  <value:"bool"> => {
    QuintEx::QuintBool{id: helper.get_id(), value}
  },
  <name:"identifier"> => {
    QuintEx::QuintName{id: helper.get_id(), name: name.into()}
  },
   "(" <e:QuintEx> ")" => e
}



// Lexer
extern {
  type Location = usize;
  type Error = LexicalError;

  enum Token {
    "int" => Token::Int(<i64>),
    "str" => Token::String(<String>),
    "bool" => Token::Bool(<bool>),

    "identifier" => Token::LowId(<String>),
    "cap_id" => Token::CapId(<String>),

    "+" => Token::Add,
    "-" => Token::Sub,
    "*" => Token::Mul,
    "/" => Token::Div,
    "%" => Token::Mod,

    "=" => Token::Assign,
    "==" => Token::EQ,
    "!=" => Token::NE,
    ">" => Token::GT,
    "<" => Token::LT,
    ">=" => Token::GE,
    "<=" => Token::LE,

    "," => Token::Comma,
    ":" => Token::Colon,
    ";" => Token::SemiColon,
    "::" => Token::DoubleColon,
    "." => Token::Period,
    "'" => Token::Prime,
    "|" => Token::Pipe,
    "->" => Token::Arrow,
    "=>" => Token::DoubleArrow,
    "^" => Token::Hat,
    "_" => Token::Underscore,
    "..." => Token::Ellipsis,

    "(" => Token::LParen,
    ")" => Token::RParen,
    "{" => Token::LBrace,
    "}" => Token::RBrace,
    "[" => Token::LBracket,
    "]" => Token::RBracket,

    "module" => Token::Module,
    "const" => Token::Const,
    "assume" => Token::Assume,
    "var" => Token::Var,
    "val" => Token::Val,
    "def" => Token::Def,
    "pure" => Token::Pure,
    "action" => Token::Action,
    "temporal" => Token::Temporal,
    "nondet" => Token::Nondet,
    "run" => Token::Run,

    "all" => Token::All,
    "any" => Token::Any,
    "all" => Token::All,
    "if" => Token::If,
    "iff" => Token::Iff,
    "else" => Token::Else,
    "and" => Token::And,
    "or" => Token::Or,
    "implies" => Token::Implies,
    "match" => Token::Match,

    "Set" => Token::Set,
    "List" => Token::List,

    "import" => Token::Import,
    "export" => Token::Export,

    "from" => Token::From,
    "as" => Token::As,

    "type" => Token::Type,

    "/*" => Token::Comment,
  }
}
